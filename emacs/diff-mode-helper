#!/usr/bin/perl

# Helper to smooth over differences between git and mercurial.
# TODO(sdh): not bothering w/ copy/rename detection for now.

use strict;
use warnings;

use IPC::Run qw/run/;

# Perform P4DIFF helper functionality
if ($ENV{P4DIFF} eq $0) {
  while (@ARGV > 2) {
    shift;
    my $a = shift;
    my $b = shift;
    system 'diff', '-U0', $a, $b;
  }
  exit;
}

my @HG_ROOT = qw/hg root/;
my @HG_DIFF = qw/hg diff --hidden -U0 -r/;

my @GIT_TOP = qw/git rev-parse --show-toplevel/;
my @GIT_DIFF = qw/git diff -U0/;

$ENV{P4DIFF} = $0;
my @P4_TOP = qw/p4 info/;
my @P4_TOP_FILTER = ('sed', '-n', 's/Client root: //p');
my @P4_DIFF = qw/p4 diff/;

my ($root, $err);
my $hg = 0;
my $git = 0;
my $p4 = 0;

run \@HG_ROOT, '>', \$root, '2>', \$err and $hg = 1
  or run \@GIT_TOP, '>', \$root, '2>>', \$err and $git = 1
  or run \@P4_TOP, '|', \@P4_TOP_FILTER, '>', \$root, '2>>', \$err and $p4 = 1
  or die "Could not find root: $err";
chomp $root;

my $rev = shift || ($hg ? '.' : $git ? 'HEAD' : $p4 ? '' : die);
my @command = $hg ? @HG_DIFF : $git ? @GIT_DIFF : $p4 ? @P4_DIFF : die;
push @command, $rev unless $p4;

my $diff;
run \@command, '>', \$diff or die 'Could not run diff';

my ($added, $removed, $file, $mode);
for (split /\n/, $diff) {
  if (m$^--- (?:(?:a|.*/depot)/([^\t]*)|/dev/null)(?:\t.*)?$) {
    printf "%s %4s %4s %s\n", $mode, $added, $removed, $file if $mode;
    $added = $removed = 0;
    $file = $1;
    $mode = $file ? 'change' : 'create';
  } elsif (m$^\+\+\+ (?:(?:b|.*/depot)/([^\t]*)|/dev/null)(?:\t.*)?$) {
    $file = $file || $1;
    $mode = 'delete' unless $1;
  } elsif (/^[+-]/) {
    $added++ if $& eq '+';
    $removed++ if $& eq '-';
  }
}
printf "%s %4d %4d %s\n", $mode, $added, $removed, $file if $mode;

# print "ROOT=$root, hg=$hg, git=$git\n";
